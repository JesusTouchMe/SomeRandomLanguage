Grammar rules. First one at bottom


expr			        : KEYWORD:VAR IDENTIFIER ASSIGN expr
						: comp-expr ((KEYWORD:AND|KEYWORD:OR) comp-expr)*

comp-expr               : NOT comp-expr
						: arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr	            : term ((PLUS|MINUS) term)*

term				    : factor ((MUL|DIV) factor)*

factor			        : (PLUS|MINUS) factor
						: power

power				    : call (POW factor)*

call				    : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom 			    	: INT|FLOAT|STRING|IDENTIFIER
						: LPAREN expr RPAREN
						: list-expr
						: if-expr
						: for-expr
						: while-expr
						: func-def

list-expr		        : LBRACKET (expr (COMMA expr)*)? RBRACKET

if-expr		        	: KEYWORD:IF expr LBRACE exp RBRACE
							(KEYWORD:ELIF expr LBRACE expr RBRACE)*
							(KEYWORD:ELSE LBRACE expr RBRACE)?

for-expr	        	: KEYWORD:FOR IDENTIFIER ASSIGN expr KEYWORD:TO expr
							(KEYWORD:STEP expr)? LBRACE expr RBRACE

while-expr          	: KEYWORD:WHILE expr LBRACE expr RBRACE

func-def		        : KEYWORD:FUN IDENTIFIER?
							LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
							ARROW? LBRACE expr RBRACE